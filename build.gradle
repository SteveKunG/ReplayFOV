plugins {
    id "fabric-loom" version "1.7-SNAPSHOT"
}

apply from: "https://raw.githubusercontent.com/SteveKunG/GradleScripts/main/fabric.gradle"

loom {
    runs.configureEach {
        properties project.commonProperties
        vmArgs project.commonVmArgs
        vmArgs project.hotswapVmArgs
    }

    runs {
        client {
            if (project.hasProperty("devauth_dir")) {
                property "devauth.configDir", "${devauth_dir}"
            }
        }
        server {
            ideConfigGenerated = false
        }
    }
}

repositories {
    ivy {
        setName("ReplayMod")
        setUrl("https://minio.replaymod.com/replaymod/")
        patternLayout { artifact("[artifact]-[revision](-[classifier])(.[ext])") }
        metadataSources { artifact() }
        content { includeGroup("com.replaymod") }
    }
}

dependencies {
    modImplementation "com.replaymod:replaymod-${minecraft_version}:${replay_version}"
}

String fileName = "${archives_base_name}-mc${supported_minecraft_version}"

remapJar {
    archiveVersion = "v${mod_version}"
    archiveBaseName = fileName
}